/*!
 *  Bayrell Core Library
 *
 *  (c) Copyright 2018-2019 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
 
namespace Core.Components.Tree;

use Runtime.Reference;
use Core.Annotations.BindModel;
use Core.Annotations.BindValue;
use Core.Annotations.Event;
use Core.Annotations.EventAsync;
use Core.Components.Dialog.DialogModel;
use Core.Components.Dialog.DialogResultEvent;
use Core.Components.ContextMenu.ContextMenuItemClickEvent;
use Core.Components.ContextMenu.ContextMenuModel;
use Core.Components.Tree.TreeDialogModel;
use Core.Components.Tree.TreeItem;
use Core.Components.Tree.TreeItemEvent;
use Core.Components.Tree.TreeModel;
use Core.UI.Events.MouseEvent.MouseClickEvent;
use Core.UI.Events.MouseEvent.MouseContextMenuEvent;
use Core.UI.Events.MouseEvent.MouseDoubleClickEvent;
use Core.UI.Events.UserEvent.ChangeEvent;
use Core.UI.CoreManager;


class TreeManager extends CoreManager
{
	public TreeModel model;
	public Reference ref_contextmenu = null;
	public Reference ref_dialog = null;
	
	
	/**
	 * Create new item
	 */
	public TreeItem newItem(Dict map)
	{
		return new TreeItem(map);
	}
	
	
	
	/**
	 * Update item by path
	 */
	public void updateItem(Collection<int> path, Dict data)
	{
		NavigatorItem item = TreeModel::getTreeItem(this.model, path);
		item = item.copy(data);
		this.setModel( TreeModel::setTreeItem(this.model, path, item) );
	}
	
	
	
	/**
	 * Select item by path
	 */
	public void selectItem(string tree_data_path)
	{
		this.updateModel({
			"selected": tree_data_path,
		});
	}
	
	
	
	/* ---------------------------------- Events ---------------------------------- */
	
	
	/**
	 * Event item click
	 */
	public void onTreeItemLabelClick(UIEvent<MouseClickEvent> e)
	{
		string tree_data_path = e.event.ui.props.get("data-path");
		
		/* Select node */
		Model model = this.model;
		model = TreeModel::selectItem(model, tree_data_path);
		this.setModel(model);
	}
	
	
	
	/**
	 * Event item click
	 */
	public void onTreeItemLabelContextMenu(UIEvent<MouseContextMenuEvent> e)
	{
		string tree_data_path = e.event.ui.props.get("data-path");
		
		/* Show context menu and select item */
		this.ref_contextmenu.ref.show(e.event.pageX - window.scrollX, e.event.pageY - window.scrollY);
		this.selectItem(tree_data_path);
	}
	
	
	
	/**
	 * Event item double click
	 */
	public void onTreeItemLabelDoubleClick(UIEvent<MouseClickEvent> e)
	{
		string tree_data_path = e.event.ui.props.get("data-path");
		Collection<int> path = TreeModel::splitPath(tree_data_path);
		this.openItem(path);
	}
	
	
	
	
	/* ---------------------------------- Actions ---------------------------------- */
	
	
	/**
	 * Navigation menu item click
	 */
	public void onContextMenuClick(UIEvent<ContextMenuItemClickEvent> e)
	{
		/* Hide context menu */
		Model model = this.model;
		model <= contextmenu <= ContextMenuModel::hide(model.contextmenu);
		this.setModel(model);
		
		string tree_data_path = this.model.selected;
		Collection<int> path = TreeModel::splitPath(tree_data_path);
		if (e.event.tag == 'action_create_file')
		{
			static::showCreateFile(path);
		}
		else if (e.event.tag == 'action_create_folder')
		{
			static::showCreateFolder(path);
		}
		else if (e.event.tag == 'action_rename_item')
		{
			static::showRenameItem(path);
		}
		else if (e.event.tag == 'action_delete_item')
		{
			static::showDeleteItem(path);
		}
		else if (e.event.tag == 'action_reload_item')
		{
			static::reloadItem(path);
		}
		
	}
	
	
	
	/**
	 * Dialog create result
	 */
	public void onDialogResult(UIEvent<DialogResultEvent> e)
	{
		DialogResultEvent event = e.event;
		if (event.value == DialogModel::BUTTON_RESULT_OK)
		{
			/* Create file or folder */
			if (
				event.model.action == TreeModel::DIALOG_CREATE_FILE or
				event.model.action == TreeModel::DIALOG_CREATE_FOLDER
			)
			{
				static::createDialogItem(event);
			}
			
			/* Rename node */
			if (event.model.action == TreeModel::DIALOG_RENAME_ITEM)
			{
				static::renameDialogItem(event);
			}
			
			/* Remove node */
			if (event.model.action == TreeModel::DIALOG_DELETE_ITEM)
			{
				static::removeDialogItem(event);
			}
			
			/* Reload node */
			if (event.model.action == TreeModel::DIALOG_RELOAD_ITEM)
			{
				/*static::reloadItem(event);*/
			}
		}
	}
	
	
	
	/* ---------------------------------- Show dialog ---------------------------------- */
	
	
	/**
	 * Create new file
	 */
	public void showCreateFile(Collection<int> path)
	{
		TreeItem item = TreeModel::getTreeItem(this.model, path);
		string file_path = TreeModel::getNamePath(this.model, path);
		
		if (item.kind == TreeItem::KIND_FOLDER)
		{
			this.ref_dialog.ref.show
			({
				"action": TreeModel::DIALOG_CREATE_FILE,
				"style": DialogModel::STYLE_PROMT,
				"is_modal": true,
				"title": "",
				"message": "Create file in " ~ file_path,
				"tag": path,
			});
		}
		else
		{
			this.ref_dialog.ref.show
			({
				"style": DialogModel::STYLE_ALERT,
				"is_modal": true,
				"title": "Warning",
				"message": "File must be folder",
				"tag": path,
			});
		}
		
	}
	
	
	
	/**
	 * Show create new folder
	 */
	public void showCreateFolder(Collection<int> path)
	{
		TreeItem item = TreeModel::getTreeItem(this.model, path);
		string file_path = TreeModel::getNamePath(this.model, path);
		
		if (item.kind == TreeItem::KIND_FOLDER)
		{
			this.ref_dialog.ref.show(
				{
					"action": TreeModel::DIALOG_CREATE_FOLDER,
					"style": DialogModel::STYLE_PROMT,
					"is_modal": true,
					"title": "",
					"message": "Create folder in " ~ file_path,
					"tag": path,
				}
			);
		}
		else
		{
			this.ref_dialog.ref.show(
				{
					"style": DialogModel::STYLE_ALERT,
					"is_modal": true,
					"title": "Warning",
					"message": "File must be folder",
					"tag": path,
				}
			);
		}
	}
	
	
	
	/**
	 * Rename node
	 */
	public void showRenameItem(Collection<int> path)
	{
		TreeItem item = TreeModel::getTreeItem(this.model, path);
		string file_path = TreeModel::getNamePath(this.model, path);
		string mesage = "";
		
		if (item == null)
		{
			return;
		}
		
		if (item.kind == TreeItem::KIND_FOLDER)
		{
			mesage = "Rename folder " ~ file_path;
		}
		else if (item.kind == TreeItem::KIND_FILE)
		{
			mesage = "Rename file " ~ file_path;
		}
		
		if (mesage)
		{
			this.ref_dialog.ref.show(
				{
					"action": TreeModel::DIALOG_RENAME_ITEM,
					"style": DialogModel::STYLE_PROMT,
					"input": item.name,
					"is_modal": true,
					"title": "",
					"message": mesage,
					"tag": path,
				}
			);
		}
		
	}
	
	
	
	/**
	 * Show delete node
	 */
	public void showDeleteItem(Collection<int> path)
	{
		TreeItem item = TreeModel::getTreeItem(this.model, path);
		string file_path = TreeModel::getNamePath(this.model, path);
		string mesage = "";
		
		if (item == null)
		{
			return;
		}
		
		if (item.kind == TreeItem::KIND_FOLDER)
		{
			mesage = "Delete folder " ~ file_path ~ "?";
		}
		else if (item.kind == TreeItem::KIND_FILE)
		{
			mesage = "Delete file " ~ file_path ~ "?";
		}
		
		if (mesage)
		{
			this.ref_dialog.ref.show(
				{
					"action": TreeModel::DIALOG_DELETE_ITEM,
					"style": DialogModel::STYLE_CONFIRM,
					"is_modal": true,
					"title": "",
					"message": mesage,
					"tag": path,
				}
			);
		}
		
	}
	
	
	
	/* ---------------------------------- Reload Item ---------------------------------- */
	
	
	/**
	 * Reload item
	 */
	void reloadItem(Collection<int> path)
	{
	}
	
	
	
	/* ---------------------------------- Open Item ---------------------------------- */
	
	
	
	/**
	 * Open item
	 */
	void openItem(Collection<int> path)
	{
		TreeItem item = TreeModel::getTreeItem(this.model, path);
		
		if (item.kind == TreeItem::KIND_FOLDER)
		{
			/* Update model */
			this.updateItem(
				path,
				{
					"is_open": not item.is_open,
					"is_reload": false,
				}
			);
			
			/* Signal Out */
			this.signalOut( new TreeItemEvent{ "kind": TreeItemEvent::OPEN, "path": path, "item": item } );
		}
	}
	
	
	
	
	
	/* ---------------------------------- Create Item ---------------------------------- */
	
	
	
	/**
	 * Create item by dialog result
	 */
	public TreeItem createNewItemByDialogResult(DialogResultEvent result)
	{
		string name = result.input;
		string kind = "";
		DialogModel model = result.model;
		
		if (model instanceof DialogModel)
		{
			if (model.action == TreeModel::DIALOG_CREATE_FILE) kind = TreeItem::KIND_FILE;
			else if (model.action == TreeModel::DIALOG_CREATE_FOLDER) kind = TreeItem::KIND_FOLDER;
			
			TreeItem item = this.newItem({
				"name": name,
				"kind": kind,
			});
			
			return item;
		}
		
		return null;
	}
	
	
	
	/**
	 * Create item
	 */
	public void createDialogItem(DialogResultEvent result)
	{
		Collection<int> path = result.model.tag;
		TreeItem item = this.createNewItemByDialogResult(result);
		if (item != null)
		{
			TreeModel model = this.model;
			
			/* Set parent item open */
			model = TreeModel::setIsOpen(model, path, true);
			
			/* Add new item */
			model = TreeModel::addTreeItem
			(
				this.model,
				path,
				item,
				method this.pushItemToCollection
			);
			
			/* Set new model */
			this.setModel( model );
			
			/* Signal Out */
			this.signalOut( new TreeItemEvent{ "kind": TreeItemEvent::CREATE, "path": path, "item": item } );
		}
	}
	
	
	
	/* ---------------------------------- Rename Item ---------------------------------- */
	
	
	/**
	 * Rename item
	 */
	public void renameDialogItem(DialogResultEvent result)
	{
		Collection<int> path = result.model.tag;
		TreeItem item = TreeModel::getTreeItem(this.model, path);
		if (item != null)
		{
			TreeItem item_new = item;
			TreeItem item_old = item;
			item_new <= name <= result.input;
			TreeModel model = TreeModel::setTreeItem
			(
				this.model,
				result.model.tag,
				item_new
			);
			this.setModel( model );
			
			/* Signal Out */
			this.signalOut(
				new TreeItemEvent
				{
					"kind": TreeItemEvent::RENAME,
					"path": path,
					"item_new": item_new,
					"item_old": item_old,
				}
			);
		}
	}
	
	
	
	/* ---------------------------------- Delete Item ---------------------------------- */
	
	
	/**
	 * Remove item
	 */
	public void removeDialogItem(DialogResultEvent result)
	{
		Collection<int> path = result.model.tag;
		TreeItem item = TreeModel::getTreeItem(this.model, path);
		TreeModel model = TreeModel::deleteTreeItem
		(
			this.model,
			path
		);
		this.setModel( model );
	}
	
	
}